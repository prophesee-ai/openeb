apply plugin: 'com.android.application'

ext.ndk_abi = rootProject.ext.ndk_abi
ext.ndk_version = rootProject.ext.ndk_version
ext.metavision_plugin_dir = rootProject.ext.metavision_plugin_dir 
ext.build_dir = rootProject.ext.build_dir + "/app"
project.buildDir = build_dir

android {
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = rootProject.name + ".apk"
        }
    }
    signingConfigs {
        config {
            keyAlias 'prophesee'
            keyPassword 'prophesee'
            storeFile file('../prophesee.jks')
            storePassword 'prophesee'
        }
    }
    lintOptions {
        abortOnError false
    }

    compileSdk 33
    buildToolsVersion "32.0.0"
    ndkVersion ndk_version

    namespace 'prophesee.metavision.viewer'

    defaultConfig {
        applicationId "prophesee.metavision.viewer"
        minSdk 31
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk.abiFilters ndk_abi
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments '-DANDROID_CPP_FEATURES=rtti exceptions'
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            externalNativeBuild {
                cmake {
                    arguments '-DCMAKE_BUILD_TYPE=Release'
                }
            }
        }
        debug {
            externalNativeBuild {
                cmake {
                    arguments '-DCMAKE_BUILD_TYPE=Debug'
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path '../cpp/CMakeLists.txt'
            buildStagingDirectory build_dir + "/../.externalNativeBuild"
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            assets.srcDirs = [
                'src/main/assets', 
                build_dir + "/assets",
                metavision_plugin_dir
            ]
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-gcm:15.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}